// A class to build the camera matrix from the camera parameters.
class CameraMatrix {
    // Constants
    static Vec3 CONST_UP;
    
    // Variables for temporal computations
    static Vec3 CAMERA_forward, CAMERA_right, CAMERA_up;
    
    function void init() {
        // Constants
        let CONST_UP = Vec3.new(Float316.new(0, 0, 0),       // 0.0
                                Float316.new(0, 127, 8192),  // 1.0
                                Float316.new(0, 0, 0));      // 0.0
                                
        // Variables for temporal computations
        let CAMERA_forward = Vec3.newZero();
        let CAMERA_right = Vec3.newZero();
        let CAMERA_up = Vec3.newZero();
        return;
    }
    
    // Create a camera matrix from the camera position
    // and the the look at point parameters.
    // v1, v2 and v3 are the row vectors of the matrix.
    function void create(Vec3 v1, Vec3 v2, Vec3 v3) {
        // FORWARD
        do Vec3.sub(CAMERA_forward, Parameters.cameraPos(),
                    Parameters.cameraLookAt());
        do UtilsVec.normalize(CAMERA_forward, CAMERA_forward);
        
        // RIGHT
        do UtilsVec.cross(CAMERA_right, CONST_UP, CAMERA_forward);
        do UtilsVec.normalize(CAMERA_right, CAMERA_right);
        
        // UP
        do UtilsVec.cross(CAMERA_up, CAMERA_forward, CAMERA_right);
        
        // set the result
        do v1.set(CAMERA_right.x(), CAMERA_up.x(), CAMERA_forward.x());
        do v2.set(CAMERA_right.y(), CAMERA_up.y(), CAMERA_forward.y());
        do v3.set(CAMERA_right.z(), CAMERA_up.z(), CAMERA_forward.z());
        return;
    }
}
